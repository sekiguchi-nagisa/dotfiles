source $MODULE_DIR/completion

# $COMP_HOOK
function _carapace_comp(m: Module, argv: [String], n: Int): Candidates? {
    var cmd = $argv[0]
    var compline = $argv.join(' ')
    if $n >= $argv.size() {
        $compline += ' '
    }
    let tilde = $argv.peek().startsWith("~")

    var ret : Candidates
    (if (echo ${compline}"''" | xargs echo &>> /dev/null) {
        echo ${compline}"''" | xargs carapace $cmd fish
    } elif (echo ${compline} | sed "s/\$/'/" | xargs echo &>> /dev/null) {
        echo ${compline} | sed "s/\$/'/" | xargs carapace $cmd fish
    } else {
        echo ${compline} | sed 's/$/"/' | xargs carapace $cmd fish
    }) | for line in $STDIN {
        var ss = $line.split($'\t')
        $ret.add($_quote($ss[0], $tilde), $ss.get(1))
    }
    return $ret
}

function _quote(can: String, tilde: Bool): String {
    var trim_tilde = $false
    if $tilde && $can.startsWith("~") {
        $trim_tilde = $true
        $can = $can.slice(1)
    }
    $can = $can.quote()
    if $trim_tilde {
        $can = "~" + $can
    }
    return $can
}

if (command -v carapace &>> /dev/null && command -v xargs &>> /dev/null) {
    importenv CARAPACE_IGNORE : ""
    let ignores = $CARAPACE_IGNORE.split(':').sort()
    carapace --list | for line in $STDIN {
        var cmd = $line.split(' ')[0]
        $ignores.searchSorted($cmd) > -1 && continue
        if var old = $compAdd($cmd, $_carapace_comp) {  # do not overwrite already defined completer
            $compAdd($cmd, $old)
        }
    }
}